#!/bin/csh
#
#  C-shell script to execute GAMESS test jobs, one job after the next.
#  Each output is put in its own separate log file in the gamess root.
#  Invoke this by typing 'runall &'.
#
#  A script to check the results is ~/gamess/tools/checktst/checktst
#  Edit this file to set the base path to GAMESS, then execute it to be
#  sure all tests TERMINATED NORMALLY, and got correct numbers too.
#
#
if (-e install.info) then
   source install.info
else
   echo "Please run 'config' first, to set up GAMESS compiling information"
   exit 4
endif
#
chdir $GMS_PATH
if(null$1 == null) then
  set VERNO=00
else
  set VERNO=$1
endif
#
#  Loop over all Ntest jobs supplied with GAMESS
#  The jobs are run in batch queues if a very old batch queue program
#  named DQS is found on the system, that's for Iowa State's group,
#  otherwise the test is executed interactively by 'rungms'.
#
set i=1
set NCPUS=1
set Ntest=47
while ($i <= $Ntest)
  if ($NCPUS > 1) then
    if($i ==  5) goto skip
    if($i == 23) goto skip
    if($i == 25) goto skip
    if($i == 27) goto skip
    if($i == 32) goto skip
    if($i == 39) goto skip
    if($i == 42) goto skip
    if($i == 45) goto skip
    if($i == 46) goto skip
    if($i == 47) goto skip
  endif
               set NUM=$i
  if ($i <= 9) set NUM=0$i
  if (-e /usr/local/bin/dqs_execd) then
     gms -v $VERNO -q ti -l exam$NUM.log exam$NUM
     sleep 5
  else
    echo Running exam$NUM
     ./rungms.windows exam$NUM $VERNO $NCPUS 0  >&  exam$NUM.log
     sleep 5
  endif
skip:  
  @ i++
end
#
tools/checktst/checktst
