#!/bin/csh
#
#   Unix on-line help - written by Greg Atchity, 25 March 1993
#
#   This script makes it simple to display the GAMESS input keywords in
#   the file INPUT.DOC online, in the spirit of the Unix 'man' command.
#   Plain text, quickly displayed, and just as quickly exited.
#
#   This script should be made available to all users, which is very easy:
#      a) make sure that INPUT.DOC is world readable, chmod 644 INPUT.DOC
#      b) change the 'doc' variable to point to your site's actual file name
#      c) make it available, e.g. 'alias gmshelp /full/path/to/gmshelp',
#         or copy it to a location such as /usr/local/bin on people's path.
#
#   Note that the little known "b" subcommand to 'more' will allow you
#   to read backwards, just as a "space bar" lets you read forward.  The
#   only  other 'more' subcommand you need to know is "q" for quit!
#
set doc=~mike/gamess/INPUT.DOC

if ($1 == '-h' | $1 == '-H') then
  set me=$0
  echo Usage: $0 \[namelist group\]
  echo " "
  echo Don\'t include the dollar sign in the namelist group.
  echo " "
  echo $me:t uses the pager "'more'" by default but will work with "'pg'"
  echo or "'less'" by putting one of those pagers in your PAGER environment
  echo variable.
  exit 0
endif

if ($?PAGER) then
   set pager=$PAGER
else
   set pager=more
endif

#--set key=`echo $1 | tr "[a-z]" "[A-Z]"`  # English-centric case ranges

if ($#argv == 0) then
   $pager +/module:routine $doc
else
   set key=`echo $1 | tr '[:lower:]' '[:upper:]'`
   $pager "+/^.$key group" $doc
endif
