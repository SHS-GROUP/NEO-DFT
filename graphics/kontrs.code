C 22 FEB 02 - MWS - OPTION TO SKIP BOUNDING BOX AND TITLES
C 23 APR 96 - JS  - FIX -ISO- TYPO IN TRACE
C 23 NOV 91 - GJA - AVOID ZERO DIVIDES IN INTRPL
C 28 JUL 90 - JAB - ADD CSTART VARIABLE TO KONTRS ROUTINE
C 13 MAR 90 - MWS - CHANGE FROM SYMBOL TO SYMBL CALLS
C 15 MAR 87 - MWS - TOTAL CLEANUP
C
C     This code draws a two dimensional contour map.
C     Examples of such maps include:
C
C     Program      Type of map
C     -------      ----------------------------------
C     PLTORB       PLoT molecular ORBital amplitude
C     MEPMAP       Molecular Electrostatic Potential MAP
C     DENDIF       Total electron density, or DENsity DIFference map
C
C     To draw such a map, your program must call BORDER and
C     KONTRS.  See those routines for calling instructions.
C
C     History:
C     This set of subroutines was taken from the orbital
C     plotting program PLTORB developed at Iowa State University.
C     Its probable origin was in the X-ray diffraction group
C     at Iowa State, in the 1960's.  It was set up as a independent
C     utility routine callable by many programs in March 1987
C     by Mike Schmidt at North Dakota State University.  The
C     changes include making this code resemble FORTRAN-77,
C     rather than a bowl of spaghetti (no computed GO TO's!).
C     -----------------------------------------------------------
      SUBROUTINE BORDER(BOX,WIDTH,HEIGHT,SCALE,XO,T,XIMIN,
     *                  IBOND,JBOND,NBONDS,XX,YY,XC,YC,ZC,NATOMS,
     *                  LAB1,LAB2,PROGM,BIRTH)
C     -----------------------------------------------------------
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      LOGICAL BOX
      DIMENSION XIMIN(2),T(3,3),XO(3),
     *          XX(NATOMS),YY(NATOMS),XC(NATOMS),YC(NATOMS),ZC(NATOMS),
     *          IBOND(*),JBOND(*)
      REAL X,Y,XBOX,YBOX
      CHARACTER*6  PROGM
      CHARACTER*12 ONEBHR
      CHARACTER*24 BIRTH
      CHARACTER*40 LAB1,LAB2
      DATA ONEBHR/'ONE BOHR IS '/
C
C     ----- DRAW BOX, DO TITLING, MAYBE MOLECULAR SKELETON -----
C
C        CALLING ARGUMENTS:
C     WIDTH  - THE WIDTH OF THE PLOT, IN "INCHES"
C     HEIGHT - THE HEIGHT OF THE PLOT, IN "INCHES"
C     SCALE  - SCALING FACTOR, NUMBER OF INCHES PER BOHR
C     XO     - TRANSLATION FROM MASTER TO PLOTTING COORDINATES,IN BOHR
C     T      - ORTHOGONAL ROTATION FROM MASTER TO PLOTTING COORDINATES
C     XIMIN  - MINIMUM VALUE OF PLOTTING COORDINATES, IN BOHR
C              1 FOR SOFTCOPY (MINIMAL TITLING)
C     IBOND,JBOND - ATOM PAIRS WHO WILL BE BONDED
C     NBONDS - NUMBER OF BONDS TO BE DRAWN, GIVE AS ZERO TO
C              AVOID THE DRAWING OF THE MOLECULAR SKELETON.
C     XX,YY  - WORK ARRAYS, FOR ATOMIC COORDS ON PLOTTING PLANE
C     XC,YC,ZC - ATOMIC COORDINATES IN THE MASTER COORDS, IN BOHR
C     NATOMS - THE TOTAL NUMBER OF ATOMS.  GIVE AS ZERO IF THIS
C              IS NOT SOME SORT OF MOLECULAR MAP.
C     LAB1,LAB2 - TWO 40 CHARACTER LABELS FOR THE PLOT
C     PROGM  - 6 CHARACTER NAME OF THE CALLING PROGRAM
C     BIRTH  - 24 CHARACTER TIME STAMP STRING FOR THE PLOT
C
C           LABEL THE PLOT.
C
      IF(BOX) THEN
         CALL SYMBL( 0.25E+00,0.75E+00,0.250E+00,LAB1  ,90.0E+00,40)
         CALL SYMBL( 0.65E+00,0.75E+00,0.250E+00,LAB2  ,90.0E+00,40)
         CALL SYMBL( 1.10E+00,0.00E+00,0.200E+00,PROGM ,90.0E+00, 6)
         CALL SYMBL( 1.10E+00,1.75E+00,0.125E+00,BIRTH ,90.0E+00,24)
         CALL SYMBL( 1.10E+00,5.00E+00,0.200E+00,ONEBHR,90.0E+00,11)
         CALL PLOT ( 0.90E+00,7.50E+00,3)
         CALL PLOT ( 1.10E+00,7.50E+00,2)
         CALL PLOT ( 1.00E+00,7.50E+00,3)
         Y = (7.50D+00 + SCALE)
         CALL PLOT ( 1.00E+00,Y  ,2)
         CALL PLOT ( 0.90E+00,Y  ,3)
         CALL PLOT ( 1.10E+00,Y  ,2)
      END IF
C
C           DRAW THE BOX. THE PEN ORIGIN IS SHIFTED FIRST.
C           THE ORIGIN WILL BE RESTORED AT THE END OF KONTRS.
C
      XBOX = WIDTH
      YBOX = HEIGHT
      CALL PLOT(1.50E+00 ,0.00E+00  ,-3)
C
      IF(BOX) THEN
         CALL PLOT(XBOX     ,0.00E+00  , 2)
         CALL PLOT(XBOX     ,YBOX      , 2)
         CALL PLOT(0.00E+00 ,YBOX      , 2)
         CALL PLOT(0.00E+00 ,0.00E+00  , 2)
      END IF
C
C           PROJECT THE ATOMIC COORDINATES TO THE PLOTTING PLANE.
C
      IF(NATOMS.LE.0) RETURN
      DO 200 K=1,NATOMS
         XCOORD = XC(K)
         YCOORD = YC(K)
         ZCOORD = ZC(K)
         XPLT  =(XCOORD-XO(1))*T(1,1)+(YCOORD-XO(2))*T(2,1)
     *         +(ZCOORD-XO(3))*T(3,1)
         YPLT  =(XCOORD-XO(1))*T(1,2)+(YCOORD-XO(2))*T(2,2)
     *         +(ZCOORD-XO(3))*T(3,2)
         XPLT = (XPLT + ABS(XIMIN(1)))*SCALE
         YPLT = (YPLT + ABS(XIMIN(2)))*SCALE
         XX(K)=XPLT
         YY(K)=YPLT
  200 CONTINUE
C
C        PUT TICK MARKS AT EDGE OF PLOT FOR ATOMIC PROJECTIONS
C
      IF(BOX) THEN
         Y = HEIGHT
         DO 300 K=1,NATOMS
            X = XX(K)
            CALL PLOT(X,  0.00E+00,3)
            CALL PLOT(X,  0.25E+00,2)
            CALL PLOT(X,Y         ,3)
            CALL PLOT(X,Y-0.25E+00,2)
  300    CONTINUE
         X = WIDTH
         DO 400 K=1,NATOMS
            Y = YY(K)
            CALL PLOT(  0.00E+00,Y,3)
            CALL PLOT(  0.25E+00,Y,2)
            CALL PLOT(X         ,Y,3)
            CALL PLOT(X-0.25E+00,Y,2)
  400    CONTINUE
      END IF
C
C           DRAW THE MOLECULAR SKELETON.
C
      IF(NBONDS.LE.0) RETURN
      CALL BOND(IBOND,JBOND,NBONDS,XX,YY,NATOMS)
      RETURN
      END
C     ------------------------------------------------
      SUBROUTINE BOND(IBOND,JBOND,NBONDS,XX,YY,NATOMS)
C     ------------------------------------------------
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      DIMENSION IBOND(NBONDS),JBOND(NBONDS),XX(NATOMS),YY(NATOMS)
      REAL X,Y
C
C     ----- THIS ROUTINE DRAWS THE MOLECULAR SKELETON -----
C
      DO 100 I=1,NBONDS
         N = IBOND(I)
         M = JBOND(I)
         X = XX(N)
         Y = YY(N)
         CALL PLOT(X,Y,3)
         X = XX(M)
         Y = YY(M)
         CALL PLOT(X,Y,2)
  100 CONTINUE
      CALL PLOT(0.0E+00,0.0E+00,3)
      RETURN
      END
C     ---------------------------------------------------------
      SUBROUTINE KONTRS(GRID,MESHX,MESHY,NNCON,NPCON,NZCON,DELTA,
     *                  BASE,MODE,CSTART,HEIGHT,WIDTH,THETA,PHI)
C     ---------------------------------------------------------
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      DIMENSION GRID(MESHX,MESHY)
      PARAMETER (ZERO=0.0D+00)
C
C     ----- DRAW A CONTOUR MAP -----
C
C        CALLING ARGUMENTS:
C     GRID   - THE FUNCTION TO BE PLOTTED.  ALL VALUES MUST BE DEFINED
C     MESHX,MESHY - NUMBER OF X AND Y GRID POINTS.
C     NNCON  - NUMBER OF NEGATIVE CONTOURS TO BE DRAWN
C     NPCON  - NUMBER OF POSITIVE CONTOURS TO BE DRAWN
C     NZCON  - GIVE AS 1 TO DRAW A ZERO CONTOUR LINE
C     DELTA  - ALGEBRAIC CONTOUR INCREMENT
C     BASE   - EXPONENTIAL CONTOUR INCREMENT
C     MODE   - 0 GIVES I-TH CONTOUR AT +/- DELTA*I
C              1 GIVES I-TH CONTOUR AT +/- BASE**(I-1)
C     CSTART - CONTOURS FROM ZERO TO +/- CSTART WILL NOT BE DRAWN
C              (EXCEPT FOR POSSIBLY THE ZERO CONTOUR, IF NZCON=1)
C     HEIGHT - HEIGHT OF THE PLOT
C     WIDTH  - WIDTH OF THE PLOT
C     THETA  - ANGLE X MAKES WITH HORIZONTAL (NORMALLY 0)
C     PHI    - ANGLE Y MAKES WITH HORIZONTAL (NORMALLY PI/2)
C              (THETA AND PHI ARE NOT SUPPORTED BY BORDER)
C
C
C            DRAW THE ZERO CONTOUR
C
      IF(NZCON.EQ.1)
     *    CALL SCANCT(GRID,MESHX,MESHY,ZERO,0,HEIGHT,WIDTH,THETA,PHI)
C
C            DRAW THE NEGATIVE CONTOURS, IF ANY
C
      DO 100 I = 1,NNCON
         IF(MODE .EQ. 0) CLVAL = -I*DELTA - CSTART
         IF(MODE .NE. 0) CLVAL = -(BASE**(I-1)) - CSTART
         CALL SCANCT(GRID,MESHX,MESHY,CLVAL,-1,HEIGHT,WIDTH,THETA,PHI)
  100 CONTINUE
C
C            DRAW THE POSITIVE CONTOURS, IF ANY
C
      DO 200 I = 1,NPCON
         IF(MODE .EQ. 0) CLVAL = I*DELTA + CSTART
         IF(MODE .NE. 0) CLVAL = BASE**(I-1) + CSTART
         CALL SCANCT(GRID,MESHX,MESHY,CLVAL,1,HEIGHT,WIDTH,THETA,PHI)
  200 CONTINUE
C
C            RESTORE THE PEN ORIGIN TO VALUE BEFORE BORDER CALL
C
      CALL PLOT(-1.50E+00,0.0E+00,-3)
      RETURN
      END
C     ----------------------------------------------------
      SUBROUTINE SCANCT(GRID,MESHX,MESHY,CL,ID,HT,W,TH,PH)
C     ----------------------------------------------------
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      LOGICAL FIRST
      DIMENSION GRID(MESHX,MESHY)
      COMMON /CURVE / X(1600),Y(1600),PY,CLVAL,THETA,PHI,HEIGHT,WIDTH,
     *                IDASH,NX,NY,IX,IY,IDX,IDY,INSIDE,IT,JT,NP,NQ,
     *                IRC(1200),IPT(3,3),INX(8),INY(8)
      PARAMETER (ZERO=0.0D+00, FIVE=5.0D+00)
      DATA FIRST/.TRUE./
C
C     ----- LOOK FOR GRID POINTS BRACKETING A CONTOUR LINE -----
C
      IDASH  = ID
      HEIGHT = HT
      WIDTH  = W
      THETA  = TH
      PHI    = PH
      CLVAL  = CL
      NX = MESHX
      NY = MESHY
      NP = 0
C
      IF(.NOT.FIRST) GO TO 100
         FIRST=.FALSE.
         IPT(1,1)=8
         IPT(1,2)=1
         IPT(1,3)=2
         IPT(2,1)=7
         IPT(2,3)=3
         IPT(3,1)=6
         IPT(3,2)=5
         IPT(3,3)=4
         INX(1)=-1
         INX(2)=-1
         INX(3)=0
         INX(4)=1
         INX(5)=1
         INX(6)=1
         INX(7)=0
         INX(8)=-1
         INY(1)=0
         INY(2)=1
         INY(3)=1
         INY(4)=1
         INY(5)=0
         INY(6)=-1
         INY(7)=-1
         INY(8)=-1
  100 CONTINUE
C
      DO 110 J=1,1200
         IRC(J)=0
  110 CONTINUE
C
C        LOOK ALONG EDGES OF THE GRID
C
      INSIDE=0
      DO 200 I=1,NX-1
         IF(GRID(I  ,1).GE.CLVAL) GO TO 200
         IF(GRID(I+1,1).LT.CLVAL) GO TO 200
         IX = I+1
         IY = 1
         IDX =-1
         IDY =0
         CALL TRACE(GRID,MESHX,MESHY)
  200 CONTINUE
C
      DO 300 I=1,NY-1
         IF(GRID(NX,I  ).GE.CLVAL) GO TO 300
         IF(GRID(NX,I+1).LT.CLVAL) GO TO 300
         IX = NX
         IY = I+1
         IDX =0
         IDY =-1
         CALL TRACE(GRID,MESHX,MESHY)
  300 CONTINUE
C
      DO 400 I=NX,2,-1
         IF(GRID(I  ,NY).GE.CLVAL) GO TO 400
         IF(GRID(I-1,NY).LT.CLVAL) GO TO 400
         IX =I-1
         IY =NY
         IDX =1
         IDY =0
         CALL TRACE(GRID,MESHX,MESHY)
  400 CONTINUE
C
      DO 500 I=NY,2,-1
         IF(GRID(1,I  ).GE.CLVAL) GO TO 500
         IF(GRID(1,I-1).LT.CLVAL) GO TO 500
         IX =1
         IY =I-1
         IDX =0
         IDY =1
         CALL TRACE(GRID,MESHX,MESHY)
  500 CONTINUE
C
C        LOOK AT INTERIOR OF GRID, FOR CONTOURS THAT DO
C        NOT INTERSECT THE EDGE OF THE GRID.
C
      INSIDE=1
      DO 620 J=2,NY-1
         DO 610 I=1,NX-1
            IF(GRID(I  ,J).GE.CLVAL) GO TO 610
            IF(GRID(I+1,J).LT.CLVAL) GO TO 610
            ICM=NX*(I+1)+J
            DO 600 K=1,NP
               IF(IRC(K).EQ.ICM) GO TO 610
  600       CONTINUE
            IX = I+1
            IY =J
            IDX =-1
            IDY =0
            CALL TRACE(GRID,MESHX,MESHY)
  610    CONTINUE
  620 CONTINUE
      RETURN
      END
C     ----------------------------------
      SUBROUTINE TRACE(GRID,MESHX,MESHY)
C     ----------------------------------
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      DIMENSION GRID(MESHX,MESHY)
      COMMON /CURVE / X(1600),Y(1600),PY,CLVAL,THETA,PHI,HEIGHT,WIDTH,
     *                IDASH,NX,NY,IX,IY,IDX,IDY,INSIDE,IT,JT,NP,NQ,
     *                IRC(1200),IPT(3,3),INX(8),INY(8)
      PARAMETER (ZERO=0.0D+00, ONE=1.0D+00, TWO=2.0D+00, FOUR=4.0D+00)
C
C     ----- TRACE OUT THE ENTIRE CONTOUR -----
C     IX,IY MARK A MESH POINT SOMEWHERE ON THE CONTOUR
C
      PY =ZERO
      IT=0
      JT=0
      NQ=0
      XM=NX-1
      YM=NY-1
      IXO= IX
      IYO= IY
      ISX = IDX+2
      ISY = IDY+2
      IS= IPT(ISX,ISY)
      ISO= IS
      IF(ISO.GT.8) ISO=ISO-8
C
  100 CONTINUE
      CALL INTRPL(GRID,MESHX,MESHY)
      IF(IT+JT.LE.1) GO TO 120
         XS=X(NQ-1)
         YS=Y(NQ-1)
         X(NQ-1)=X(NQ)
         Y(NQ-1)=Y(NQ)
         X(NQ)=XS
         Y(NQ)=YS
  120 CONTINUE
      IS= IS+1
      JT= IT
C
  200 CONTINUE
      IF(NQ.EQ.1600) THEN
            WRITE(6,900) CLVAL,X(NQ),Y(NQ)
            GO TO 600
         END IF
      IF(IS.GT.8) IS=IS-8
      IDX = INX(IS)
      IDY = INY(IS)
      IX2= IX+IDX
      IY2= IY+IDY
      IF(INSIDE.EQ.0) GO TO 300
C
C        HAVE WE CLOSED A CONTOUR LOOP?
C
      IF(IX.NE.IXO) GO TO 320
      IF(IY.NE.IYO) GO TO 320
      IF(IS.NE.ISO) GO TO 320
      CALL INTRPL(GRID,MESHX,MESHY)
      GO TO 600
C
C        HAVE WE COME TO AN EDGE?
C
  300 CONTINUE
      IF(IX2.EQ.0) GO TO 500
      IF(IX2.GT.NX) GO TO 500
      IF(IY2.EQ.0) GO TO 500
      IF(IY2.GT.NY) GO TO 500
C
C        NO, FIND NEXT POINT ON CONTOUR
C
  320 CONTINUE
      IF(CLVAL.GT.GRID(IX2,IY2)) GO TO 100
      IF(IDX*IDX+IDY*IDY.EQ.1) GO TO 400
      DCP=(GRID(IX,IY)+GRID(IX2,IY)+GRID(IX,IY2)+GRID(IX2,IY2))/FOUR
      IF(DCP.LT.CLVAL) GO TO 100
C
      IF(INX(IS-1).NE.0) THEN
            IX = IX+IDX
            IDX =-IDX
            PY =TWO
            CALL INTRPL(GRID,MESHX,MESHY)
            IX = IX+IDX
         ELSE
            IY = IY+IDY
            IDY =-IDY
            PY =TWO
            CALL INTRPL(GRID,MESHX,MESHY)
            IY = IY+IDY
         END IF
  400 CONTINUE
      IF(GRID(IX-1,IY).GE.CLVAL) GO TO 420
         NP=NP+1
         IRC(NP)=NX*IX+IY
  420 CONTINUE
      IS= IS+5
      IX = IX2
      IY = IY2
      GO TO 200
C
C        CONTOUR LINE IS TERMINATED ON EDGE
C
  500 CONTINUE
      IF(GRID(IX-1,IY).GE.CLVAL) GO TO 600
         NP=NP+1
         IRC(NP)=NX*IX+IY
C
C        CONTOUR LINE IS FINISHED, DRAW IT OUT
C
  600 CONTINUE
      SINTHE = SIN(THETA)
      COSTHE = COS(THETA)
      SINPHI = SIN(PHI)
      COSPHI = COS(PHI)
      WW = WIDTH/XM
      HH = HEIGHT/YM
      DO 620 I=1,NQ
         XTEMP=(X(I)-ONE)*WW*COSTHE + (Y(I)-ONE)*HH*COSPHI
         YTEMP=(X(I)-ONE)*WW*SINTHE + (Y(I)-ONE)*HH*SINPHI
         X(I) = XTEMP
         Y(I) = YTEMP
  620 CONTINUE
      CALL BLOT(X,Y,NQ,IDASH)
      RETURN
C
  900 FORMAT(//,'THE CONTOUR LINE AT VALUE',F10.4,
     *          'WAS TERMINATED AT X=',F10.4,' Y=',F10.4/
     *       1X,'BECAUSE IT CONTAINED MORE THAN 1600 PLOT POINTS.')
      END
C     -----------------------------------
      SUBROUTINE INTRPL(GRID,MESHX,MESHY)
C     -----------------------------------
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      DIMENSION GRID(MESHX,MESHY)
      COMMON /CURVE / X(1600),Y(1600),PY,CLVAL,THETA,PHI,HEIGHT,WIDTH,
     *                IDASH,NX,NY,IX,IY,IDX,IDY,INSIDE,IT,JT,NP,NQ,
     *                IRC(1200),IPT(3,3),INX(8),INY(8)
      PARAMETER (ZERO=0.0D+00, HALF=5.0D-01, TWO=2.0D+00, FOUR=4.0D+00)
C
C     ----- INTERPOLATE A POINT ON THE CONTOUR -----
C     THE ACTUAL CONTOUR PASSES BETWEEN MESH POINTS,
C     SO IT LOOKS SMOOTH.
C
      IT=0
      NQ=NQ+1
      IF(IDX*IDX+IDY*IDY.NE.1) GO TO 200
      IF(IDX.NE.0) GO TO 100
C
      X(NQ)= IX
      Z= IY
      IY2= IY+IDY
      DY = IDY
      Y(NQ)=((GRID(IX,IY)-CLVAL)/(GRID(IX,IY)-GRID(IX,IY2)))*DY+Z
      RETURN
C
  100 CONTINUE
      Y(NQ)= IY
      W= IX
      DX = IDX
      IX2= IX+IDX
      X(NQ)=((GRID(IX,IY)-CLVAL)/(GRID(IX,IY)-GRID(IX2,IY)))*DX+W
      RETURN
C
  200 CONTINUE
      IX2= IX+IDX
      IY2= IY+IDY
      W= IX
      Z= IY
      DX = IDX
      DY = IDY
      DCP=(GRID(IX,IY)+GRID(IX2,IY)+GRID(IX,IY2)+GRID(IX2,IY2))/FOUR
      IF(PY.EQ.TWO) GO TO 300
      IF(DCP.GT.CLVAL) GO TO 400
      IF(ABS(GRID(IX,IY)-DCP) .LT. 1.0D-08) GO TO 400
C
  300 CONTINUE
      AL=GRID(IX,IY)-DCP
      V=HALF*(AL+DCP-CLVAL)/AL
      X(NQ)=V*DX+W
      Y(NQ)=V*DY+Z
      PY =ZERO
      RETURN
C
  400 CONTINUE
      IT=1
      AL=GRID(IX2,IY2)-DCP
      V=HALF*(AL+DCP-CLVAL)/AL
      X(NQ)=-V*DX+W+DX
      Y(NQ)=-V*DY+Z+DY
      RETURN
      END
C     ----------------------------
      SUBROUTINE BLOT(X,Y,N,IDASH)
C     ----------------------------
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      DIMENSION X(N),Y(N)
      REAL XX,YY
      PARAMETER (ZERO=0.0D+00)
C
C     ----- DRAW THE CONTOUR LINE -----
C
C     THE CONTOUR CURVE X,Y CONTAINS N POINTS.
C     IF IDASH IS NEGATIVE, DRAW A DOTTED LINE.
C     IF IDASH IS POSITIVE, DRAW A CONTINUOUS LINE.
C     IF IDASH IS ZERO, DRAW A LINE WITH WIDELY SPACED DASHES.
C
      XX = X(1)
      YY = Y(1)
      CALL PLOT(XX,YY,3)
      I=2
      IF(I.GT.N) RETURN
      IF(IDASH.NE.0) GO TO 200
C
C     DRAW DASHED LINE BY JUST OMITTING MOST OF THE POINTS.
C
  100 CONTINUE
         XX = X(I-1)
         YY = Y(I-1)
         CALL PLOT(XX,YY,3)
         XX = X(I)
         YY = Y(I)
         CALL PLOT(XX,YY,2)
         I= I+5
         IF(I.GT.N) RETURN
      GO TO 100
C
  200 CONTINUE
      IPEN=2
      IF(IDASH.LT.0) IPEN=3
  210 CONTINUE
         XX = X(I)
         YY = Y(I)
         CALL PLOT(XX,YY,2)
         I= I+1
         IF(I.GT.N) RETURN
         XX = X(I)
         YY = Y(I)
         CALL PLOT(XX,YY,2)
         I= I+1
         IF(I.GT.N) RETURN
         XX = X(I)
         YY = Y(I)
         CALL PLOT(XX,YY,IPEN)
         I= I+1
         IF(I.GT.N) RETURN
         XX = X(I)
         YY = Y(I)
         CALL PLOT(XX,YY,IPEN)
         I= I+1
         IF(I.GT.N) RETURN
      GO TO 210
      END
