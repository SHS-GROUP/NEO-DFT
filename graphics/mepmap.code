C 22 FEB 02 - MWS - OPTION TO SKIP BOUNDING BOX AND TITLES
C 26 APR 93 - MWS - DROP PANSOPHICS VERSION
C 16 JUN 92 - MWS - MAKE SURE THIS PROGRAM RUNS SERIALLY
C  7 JAN 92 - MWS - ADD POSTSCRIPT VERSION
C 20 MAY 90 - MWS - X-WINDOWS VERSION
C 14 JUL 88 - MWS - MESHMX FROM 61X61 TO 100X100, FIX CONTOURING MODE
C  7 JUL 88 - MWS - ADD SIZE KEYWORD
C  9 MAR 87 - MWS - PORT TO VAX/VMS
C 15 NOV 85 - KKB - CREATE MEPMAP
C     --------------
      PROGRAM MEPMAP
C     --------------
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      REAL SIZE
      CHARACTER*24 BIRTH
      CHARACTER*40 LAB1,LAB2
      LOGICAL BOX
*XW   INTEGER WNAME(3)
*PS   INTEGER WNAME(3)
      PARAMETER (NBMAX=200, NATMAX=50, MESHMX=100*100)
      DIMENSION IBOND(NBMAX),JBOND(NBMAX),GRID(MESHMX),
     *          XC(NATMAX),YC(NATMAX),ZC(NATMAX),XX(NATMAX),YY(NATMAX),
     *          T(3,3),XO(3),XIMIN(2),XIMAX(2)
      LOGICAL GOPARR,DSKWRK,MASWRK
      COMMON /PAR   / ME,MASTER,NPROC,IBTYP,IPTIM,GOPARR,DSKWRK,MASWRK
      PARAMETER (ZERO=0.0D+00, PI=3.14159 26535D+00, TWO=2.0D+00,
     *           BIG=1.0D+10)
*XW   DATA WNAME/4HMepm,4Hap  ,0/
*PS   DATA WNAME/4HMepm,4Hap  ,0/
C
C     ----- PLOT A MOLECULAR ELECTROSTATIC POTENTIAL MAP -----
C
C     THE ELECTROSTATIC POTENTIAL MAP IS CALCULATED BY THE PROGRAM
C     GAMESS, WHICH PUNCHES THE DATA FOR UNIT -IINP- IN EXACTLY
C     THE FORMAT EXPECTED BY THIS PROGRAM.
C
C     THIS PROGRAM WAS WRITTEN IN NOVEMBER 1985 BY KIM BALDRIDGE
C     AT NORTH DAKOTA STATE UNIVERSITY.
C
C           THIS PROGRAM RUNS IN SEQUENTIAL MODE
C
      ME = 0
      MASTER = 0
      NPROC = 1
      MASWRK = ME.EQ.MASTER
      DSKWRK = .FALSE.
C
C           DEFINE FILES USED, OPEN CARD INPUT AND PRINT OUTPUT
C
      IINP  = 1
      IOUT  = 2
      IR    = 5
      IW    = 6
      CALL OPNSEQ(IINP,'MEPMAP',    'OLD', .TRUE., .TRUE.)
      CALL OPNSEQ(IOUT,'MEPLOG','UNKNOWN',.FALSE., .TRUE.)
      WRITE(IOUT,900)
      CALL OPNCRD(IINP,IOUT)
C
C           READ INPUT OPTIONS, MOLECULAR SKELETON
C
      CALL OPTMEP(IOUT,LAB1,LAB2,MODE,IGRID,CUTOFF,DELTA,BASE,
     *            BOX,SIZE,NATOMS,NBONDS)
      IF(NBONDS.GT.0) CALL SKELTN(IOUT,IBOND,JBOND,NBONDS,NBMAX)
C
C           READ IN ELECTROSTATIC POTENTIAL MAP AND ORIENTATION
C
      CALL GETMEP(IINP,IOUT,GRID,MESHX,MESHY,MESHMX,IGRID,
     *            XC,YC,ZC,NATOMS,NATMAX,T,XO,XIMIN,XIMAX,
     *            LAB1,LAB2)
C
C           SCALE THE PLOT
C     THE PLOT AREA IS 13.3X10 INCHES ON A 640X480 PIXEL SCREEN.
C     HOWEVER, 1.5 INCHES OF THE X DIMENSION GOES TO TITLING.
C
      XMX = 11.8
      YMX = 10.0
      SCAL1  = XMX/(XIMAX(1)-XIMIN(1))
      SCAL2  = YMX/(XIMAX(2)-XIMIN(2))
      SCALE  = MIN(SCAL1,SCAL2)
      HEIGHT = SCALE*(XIMAX(2)-XIMIN(2))
      WIDTH  = SCALE*(XIMAX(1)-XIMIN(1))
      THETA  = ZERO
      PHI    = PI/TWO
C
C           DETERMINE NUMBER OF CONTOURS
C
      GMIN = BIG
      GMAX =-BIG
      DO 100 I=1,MESHX*MESHY
         GMAX = MAX(GMAX,GRID(I))
         GMIN = MIN(GMIN,GRID(I))
  100 CONTINUE
      GGMAX = MIN( CUTOFF,GMAX)
      GGMIN = MAX(-CUTOFF,GMIN)
      IF(MODE.EQ.0) THEN
            NPCON = ABS(GGMAX)/DELTA
            NNCON = ABS(GGMIN)/DELTA
            IF(GGMAX.LE.ZERO) NPCON = 0
            IF(GGMIN.GE.ZERO) NNCON = 0
            CONMAX =  DELTA*NPCON
            CONMIN = -DELTA*NNCON
         END IF
      IF(MODE.EQ.1) THEN
            NPCON = INT(LOG(ABS(GGMAX))/LOG(BASE) + 1.01)
            NNCON = INT(LOG(ABS(GGMIN))/LOG(BASE) + 1.01)
            IF(GGMAX.LE.ZERO) NPCON = 0
            IF(GGMIN.GE.ZERO) NNCON = 0
            CONMAX =  BASE**(NPCON-1)
            IF(NPCON.EQ.0) CONMAX=ZERO
            CONMIN = -BASE**(NNCON-1)
            IF(NNCON.EQ.0) CONMIN=ZERO
         END IF
      NZCON=0
      IF(GMAX*GMIN.LT.ZERO) NZCON=1
      NCTOT = NNCON + NPCON + NZCON
      CALL DATEOF(BIRTH)
      WRITE(IOUT,910) BIRTH
      WRITE(IOUT,920)
      WRITE(IOUT,930) NCTOT
      WRITE(IOUT,940) GMIN,GMAX,CONMIN,CONMAX
      IF(NCTOT.LE.1) WRITE(IOUT,950)
      IF(NCTOT.LE.1) STOP
C
C     ----- OPEN CALCOMP PLOTTING SYSTEM -----
C
C
C      X-WINDOWS BOUNDARIES 1/2 INCH WIDER THAN 13.3 X 10.0,
C      ORIGIN IS RAISED 1/4 INCH FROM LOWER LEFT CORNER.
C
*XW   CALL OPENCC(13.8,10.5,1,WNAME)
*XW   CALL PLOT(0.25,0.25,-3)
*XW   CALL FACTOR(SIZE)
C
*PS   CALL OPENPS(13.8,10.5,1,WNAME)
*PS   CALL PLOT(0.25,0.25,-3)
*PS   CALL FACTOR(SIZE)
C
C        DRAW CONTOUR PLOT
C
      CALL BORDER(BOX,WIDTH,HEIGHT,SCALE,XO,T,XIMIN,IBOND,
     *            JBOND,NBONDS,XX,YY,XC,YC,ZC,NATOMS,LAB1,
     *            LAB2,'MEPMAP',BIRTH)
      CALL KONTRS(GRID,MESHX,MESHY,NNCON,NPCON,NZCON,DELTA,BASE,
     *            MODE,ZERO,HEIGHT,WIDTH,THETA,PHI)
C
C        CLOSE CALCOMP PLOTTING SYSTEM
C
C
*XW   CALL FLSHCC
*XW   WRITE(IW,*) 'Hit <return> to exit.'
*XW   READ(IR,*)
*XW   CALL CLOSCC
C
*PS   CALL EJCTPS
*PS   CALL CLOSPS
      STOP
C
C           FORMAT STATEMENTS
C
  900 FORMAT(1X,'ELECTROSTATIC POTENTIAL MAP CONTOUR PLOTTING PROGRAM')
  910 FORMAT(1X,'CONTOUR PLOT DRAWN AT ',A)
  920 FORMAT(/1X,'CHARACTERISTICS OF THE CONTOURS')
  930 FORMAT(5X,I5,' CONTOUR LINES WILL BE DRAWN'/
     *   5X,'  CALCULATED GRID  ',5X,'   CONTOURS DRAWN'/
     *   5X,'MINIMUM     MAXIMUM',5X,'MINIMUM    MAXIMUM')
  940 FORMAT(1X,2F10.2,3X,2F10.2,' KCAL/MOL/ELECTRONCHARGE')
  950 FORMAT(/////5X,'ALL THE GRID POINTS HAVE THE SAME VALUE ',
     *   19H- NO PLOT GENERATED)
      END
C     --------------------------------------------------------------
      SUBROUTINE OPTMEP(IOUT,LAB1,LAB2,MODE,IGRID,CUTOFF,DELTA,BASE,
     *                  BOX,SIZE,NATOMS,NBONDS)
C     --------------------------------------------------------------
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      LOGICAL BOX
      REAL SIZE
      CHARACTER*40 LAB1,LAB2
      CHARACTER*4 KEYWRD
C
C        ----- READ RUN OPTIONS -----
C
      MODE= -1
      IGRID=0
      NATOMS=0
      NBONDS=0
      CUTOFF = 65.0D+00
      DELTA=5.0D+00
      BASE = 2.0D+00
      BOX = .TRUE.
      SIZE=1.0E+00
C
C        READ FRESH OPTION CARD
C
      IEOF=0
      IERR=0
  100 CONTINUE
      CALL RDCARD('OPTMEP  ',IEOF)
      IF(IEOF.NE.0) GO TO 800
C
C        SCAN NEXT KEYWORD OFF
C
  110 CONTINUE
      CALL GSTRNG(KEYWRD,-4)
      IERR=0
      IF(KEYWRD.EQ.'    ') GO TO 100
      IF(KEYWRD.EQ.'NATO') THEN
         NATOMS = IFIND('NATOMS  ',IERR)
         IF(IERR.NE.0) GO TO 800
         GO TO 110
      END IF
      IF(KEYWRD.EQ.'NBON') THEN
         NBONDS = IFIND('NBONDS  ',IERR)
         IF(IERR.NE.0) GO TO 800
         GO TO 110
      END IF
      IF(KEYWRD.EQ.'DELT') THEN
         DELTA = RFIND('DELTA   ',IERR)
         IF(IERR.NE.0) GO TO 800
         IF(MODE.EQ.1) GO TO 810
         MODE = 0
         GO TO 110
      END IF
      IF(KEYWRD.EQ.'CUTO') THEN
         CUTOFF = RFIND('CUTOFF  ',IERR)
         IF(IERR.NE.0) GO TO 800
         GO TO 110
      END IF
      IF(KEYWRD.EQ.'NOBO') THEN
         BOX=.FALSE.
         GO TO 110
      END IF
      IF(KEYWRD.EQ.'SIZE') THEN
         SIZE = RFIND('SIZE    ',IERR)
         IF(IERR.NE.0) GO TO 800
         GO TO 110
      END IF
      IF(KEYWRD.EQ.'GRID') THEN
         IGRID = 1
         GO TO 110
      END IF
      IF(KEYWRD.EQ.'BASE') THEN
         BASE=RFIND('BASE    ',IERR)
         IF(IERR.NE.0) GO TO 800
         IF(MODE.EQ.0) GO TO 810
         MODE = 1
         GO TO 110
      END IF
C
C        AN UNRECOGNIZED KEYWORD MUST BE THE TITLE CARD
C
      IF(MODE.EQ. -1) MODE=1
      CALL REREAD
      CALL GSTRNG(LAB1,40)
      CALL GSTRNG(LAB2,40)
      RETURN
C
C        SOME SORT OF INPUT BOOBOO
C
  800 CONTINUE
      WRITE(IOUT,900)
      STOP
  810 CONTINUE
      WRITE(IOUT,910)
      STOP
  900 FORMAT(I1,'**** ERROR IN OPTION INPUT, EXECUTION STOPPED ****')
  910 FORMAT(1X,'**** ERROR, BASE AND DELTA ARE MUTUALLY EXCLUSIVE')
      END
C     ------------------------------------------------
      SUBROUTINE SKELTN(IOUT,IBOND,JBOND,NBONDS,NBMAX)
C     ------------------------------------------------
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      DIMENSION IBOND(NBMAX),JBOND(NBMAX)
      CHARACTER*4 KEYWRD
C
C     ----- READ THE MOLECULAR SKELETON -----
C
      IEOF=0
      IERR=0
      IF(NBONDS.GT.NBMAX) GO TO 840
      IF(NBONDS.LT.0) GO TO 840
C
      WRITE(IOUT,900) NBONDS
      WRITE(IOUT,920)
C
      CALL RDCARD('SKELTN  ',IEOF)
      CALL GSTRNG(KEYWRD,-4)
      IF(KEYWRD.NE.'BOND') THEN
         WRITE(IOUT,*) 'EXPECTING THE -BONDATOMS- CARD!'
         CALL ABT(IOUT)
      END IF
      DO 100 I = 1,NBONDS
         IBOND(I) = IFIND('1ST ATOM',IERR)
         IF(IERR.NE.0) GO TO 800
         JBOND(I) = IFIND('2ND ATOM',IERR)
         IF(IERR.NE.0) GO TO 800
  100 CONTINUE
      RETURN
C
C        SOME SORT OF INPUT BOOBOO
C
  800 CONTINUE
      WRITE(IOUT,980)
      STOP
C
C        DIMENSION EXCEEDED
C
  840 CONTINUE
      WRITE(IOUT,990) NBONDS,NBMAX
      STOP
C
  900 FORMAT(1X,'THE NUMBER OF BONDS IS',I5)
  920 FORMAT(1X,'BOND CONNECTIONS ARE:')
  980 FORMAT(1X,'***** INPUT ERROR IN SKELTN, EXECUTION KILLED ****')
  990 FORMAT(1X,'*** ERROR, ILLEGAL NBONDS=',I5,' NBMAX=',I5)
      END
C     ----------------------------------------------------------
      SUBROUTINE GETMEP(IINP,IOUT,GRID,MESHX,MESHY,MESHMX,IGRID,
     *                  XC,YC,ZC,NATOMS,NATMAX,T,XO,XIMIN,XIMAX,
     *                  LAB1,LAB2)
C     ----------------------------------------------------------
      IMPLICIT DOUBLE PRECISION(A-H,O-Z)
      CHARACTER*40 LAB1,LAB2
      DIMENSION XC(NATMAX),YC(NATMAX),ZC(NATMAX),GRID(MESHMX),
     *          T(3,3),XO(3),XIMIN(2),XIMAX(2)
      PARAMETER (ZERO=0.0D+00, ONE=1.0D+00, TOKCAL=627.51D+00)
C
C        ----- READ ELECTROSTATIC POTENTIAL MAP DATA -----
C
      WRITE(IOUT,8000) LAB1,LAB2
C
C           READ THE NUMBER OF ATOMS
C
      READ(IINP,9010)
      IF(NATOMS.GT.NATMAX) THEN
            WRITE(IOUT,8040) NATMAX,NATOMS
            STOP
         END IF
      WRITE(IOUT,8020) NATOMS
C
C           READ COORDINATES OF EACH ATOM
C
      WRITE(IOUT,8060)
      DO 100 K=1,NATOMS
         READ(IINP,9020) XC(K),YC(K),ZC(K)
         WRITE(IOUT,8080) K,XC(K),YC(K),ZC(K)
  100 CONTINUE
C
C           GET POSITION OF PLOTTING AXES RELATIVE TO FUNDAMENTAL AXES
C
      READ(IINP,9000)
      READ(IINP,9040) XO(1),XO(2),XO(3)
      WRITE(IOUT,8100)  XO(1),XO(2),XO(3)
C
C           READ THE PLOTTING PLANE
C
      READ(IINP,9000)
      DO 200 I=1,3
         READ(IINP,9040) (T(I,J),J=1,3)
  200 CONTINUE
      WRITE(IOUT,8120)
      WRITE(IOUT,8140)((T(I,J),I=1,3),J=1,3)
C
C          NORMALIZE THE COLUMNS OF T
C
      DO 340 J=1,3
         VECSQR = ZERO
         DO 310 I=1,3
            VECSQR = VECSQR + T(I,J)*T(I,J)
  310    CONTINUE
         VECSQR = ONE/SQRT(VECSQR)
         DO 320 I=1,3
            T(I,J) = T(I,J) * VECSQR
  320    CONTINUE
  340 CONTINUE
C
C           READ THE BOUNDARIES OF THE PLOTTING PLANE
C
      READ(IINP,9000)
      READ(IINP,9060) XIMIN(1),XIMAX(1),XIMIN(2),XIMAX(2)
      WRITE(IOUT,8160)
      WRITE(IOUT,8180) (XIMIN(I),XIMAX(I),I=1,2)
C
C        READ THE NUMBER OF MAP POINTS
C
      READ(IINP,9010) MESHX,MESHY
      WRITE(IOUT,8200)  MESHX,MESHY
      IF(MESHX*MESHY.GT.MESHMX) THEN
            WRITE(IOUT,8220) MESHX,MESHY,MESHMX
            STOP
         END IF
C
C           READ ROWS OF ELECTROSTATIC POTENTIAL MAP (A.U.)
C
      READ(IINP,9000)
      DO 400 I=1,MESHX
         READ(IINP,9080) (GRID(I+(J-1)*MESHX),J=1,MESHY)
  400 CONTINUE
C
C        CONVERT MAP FROM A.U. TO KCAL/MOL/ELECTRON CHARGE
C
      LOC = 0
      DO 530 I=1,MESHX
         DO 520 J=1,MESHY
            LOC = LOC+1
            GRID(LOC)=TOKCAL*GRID(LOC)
  520    CONTINUE
  530 CONTINUE
C
C        PRINT MAP IF IGRID IS TURNED ON
C
      IF(IGRID.EQ.0) RETURN
      WRITE(IOUT,8240) (J,J=1,MESHY,3)
      DO 600 I=1,MESHX,3
         WRITE(IOUT,8260) I,(GRID(I+(J-1)*MESHX),J=1,MESHY,3)
  600 CONTINUE
      RETURN
C
 8000 FORMAT(1X,'WAVEFUNCTION TITLE IS'/1X,A40,A40)
 8020 FORMAT(1X,'THE NUMBER OF ATOMS IS',I5)
 8040 FORMAT(1X,'*** THE MAXIMUM NUMBER OF ALLOWED ATOMS IS',I5,
     *  ' YOU SPECIFIED',2X,I5/)
 8060 FORMAT(1X,'THE COORDINATES OF THE ATOMS ARE:')
 8080 FORMAT(1X,I3,3F10.5)
 8100 FORMAT(1X,'ORIGIN OF PLOTTING PLANE IS',3F10.5)
 8120 FORMAT(/13X,33H   PLOTTING PLANE AND COORDINATES//11X,
     *  58HROTATION FROM FUNDAMENTAL BASIS TO BASIS ON PLOTTING PLANE//)
 8140 FORMAT(1X,22X,3F10.5)
 8160 FORMAT(1X,10X,20HPLOT AREA BOUNDARIES/
     *     4X,'MINX   MAX X   MIN Y   MAX Y')
 8180 FORMAT(1X,4X,F6.2,2X,F6.2,2X,F6.2,2X,F6.2//)
 8200 FORMAT(1X,'MESH DIMENSIONS IN X,Y DIRECTIONS ARE',2I5)
 8220 FORMAT(1X,'MESHX=',I5,' TIMES MESHY=',I5,' EXCEEDS DIMENSION',
     *          ' LIMIT OF',I10)
 8240 FORMAT(1X,32HGRID OF ELECTROSTATIC POTENTIAL://2X,I5////
     *   5X,21I6/)
 8260 FORMAT(1X,2X,I2,2X,21F6.2)
C
 9000 FORMAT(A)
 9010 FORMAT(2I5)
 9020 FORMAT(3F16.10)
 9040 FORMAT(3E17.8)
 9060 FORMAT(4E17.8)
 9080 FORMAT(5X,5E15.7)
      END
